###### Задачи

Задача: В форме выбора товара, вызываемой только из формы документа «Расходная накладная» необходимо отображать товары, по которым есть ненулевые остатки по данным Оперативного и Бухгалтерского учета
бухгалтерского учета.  

1. В ФормеДокумента РасходнаяНакладная настроить вызов неосновной Формы выбора Справочники.Номенклатура (см. формы/вызов специальной формы)
2. Справочники.Номенклатура -> ФормаВыбора -> реквизит Список -> Свойства:
  - ПроизвольныйЗапрос = Истина
  - Настройка списка -> Открыть
3. Добавить запрос:
```
ВЫБРАТЬ
	СправочникНоменклатура.Ссылка КАК Ссылка,
	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
	СправочникНоменклатура.Код КАК Код,
	СправочникНоменклатура.Наименование КАК Наименование,
	СправочникНоменклатура.ЦенаПродажи КАК ЦенаПродажи,
	СправочникНоменклатура.Предопределенный КАК Предопределенный,
	СправочникНоменклатура.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	УправленческийОстатки.КоличествоОстатокДт КАК КоличествоОстатокДт
ИЗ
	Справочник.Номенклатура КАК СправочникНоменклатура
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки КАК ОстаткиНоменклатурыОстатки
		ПО (ОстаткиНоменклатурыОстатки.Номенклатура = СправочникНоменклатура.Ссылка)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.Товары), &ВидыСубконто, Субконто2 = &Подразделение) КАК УправленческийОстатки
		ПО (УправленческийОстатки.Субконто1 = СправочникНоменклатура.Ссылка)
ГДЕ
	(ОстаткиНоменклатурыОстатки.КоличествоОстаток <> 0
			ИЛИ УправленческийОстатки.КоличествоОстатокДт <> 0)
```
!Важно! Проверить: в настройках Списка Основная таблица = Справочник.Номенклатура

4. Перенести на Форму поля КоличествоОстаток, КоличествоОстатокДт из Списка
5. В ФормеДокумента РасходнаяНакладная настроить передачу реквизита Подразделение (имя Отдел) как Параметра (см. формы/передать-реквизит-как-параметр)
6. В модуле ФормыВыбора Справочник.Номенклатура задать параметры `&ВидыСубконто`, `&Подразделение`:
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВидыСубконто = Новый Массив;
	// задать свой порядок субконто, т.к. в режимах Конфигуратор и Пользователь порядок может быть изменен
	// порядок добавления имеет значение, первый = Субконто1
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Подразделения);
	Список.Параметры.УстановитьЗначениеПараметра("ВидыСубконто", ВидыСубконто);
	
	// получаем параметр, переданный из формы-владельца
	Список.Параметры.УстановитьЗначениеПараметра("Подразделение", Параметры.Подразделение);
	
КонецПроцедуры
```