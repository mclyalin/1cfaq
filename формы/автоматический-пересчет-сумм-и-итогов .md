###### Формы

## Автоматический пересчет сумм и итогов

-1- *Мастер создания формы Документа -> реквизит СуммаПоДокументу выключить*  
-2- *Свойства ТЧ СписокНоменклатуры -> Использование -> Подвал = Истина*  
-3- *Свойства колонки ТЧ Сумма -> Основные -> ПутьКДаннымПодвала = Объект.СписокНоменклатуры.ИтогСумма*  
-4- В общем модуле ОбработкаТабличнойЧасти:
```
Процедура РасчитатьСуммуВСтроке(ТекСтрока) Экспорт
	ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
КонецПроцедуры
```
-5- В модуле формы Документа:
```
&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РасчитатьСуммуВСтроке(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РасчитатьСуммуВСтроке(ТекСтрока);
КонецПроцедуры
```
-6- В модуле объекта Документа:
```
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры
```
## (вариант 2)

-2.1- В общем модуле ОбработкаТабличнойЧасти:
```
Процедура РасчитатьСуммуВСтроке(ТекущаяСтрока, ПересчитатьЦену = Ложь) Экспорт
	Если ПересчитатьЦену Тогда
		ТекущаяСтрока.Количество = ?(ТекущаяСтрока.Количество = 0, 1, ТекущаяСтрока.Количество);
		ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма / ТекущаяСтрока.Количество;
	Иначе
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.Количество;
	КонецЕсли;
КонецПроцедуры
```
-2.2- В модуле формы Документа:
```
&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РасчитатьСуммуВСтроке(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РасчитатьСуммуВСтроке(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыСуммаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ПересчитатьЦену = Истина;
	ОбработкаТабличнойЧасти.РасчитатьСуммуВСтроке(ТекущиеДанные, ПересчитатьЦену);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПриИзменении(Элемент)
	Объект.СуммаПоДокументу = Объект.СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры
```
-2.3- Если планируется *программное* создание документов, тогда в модуле объекта Документа:
```
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры
```